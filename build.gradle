import aQute.bnd.gradle.Bundle

plugins {
    id("java-library")
    id("maven-publish")
    id("jacoco")
    id("biz.aQute.bnd.builder") version "7.2.1"
    id("org.gradle.test-retry") version "1.6.4"
    id("io.github.gradle-nexus.publish-plugin") version "2.0.0"
    id("signing")
}

def jarVersion = "3.0.1"
group = 'io.nats'

def isRelease = System.getenv("BUILD_EVENT") == "release"
def tc = System.getenv("TARGET_COMPATIBILITY")
def targetJavaVersion = tc == null ? JavaVersion.VERSION_17 : JavaVersion.toVersion(tc)
def targetId = targetJavaVersion.toString()
def dependencyEnd = "-jdk" + targetId
def artifact = "jwt-utils-jdk" + targetId
def bundleName = "io.nats.jwt.utils.jdk" + targetId

System.out.println("targetCompatibility: " + targetId)
System.out.println("artifact: " + artifact)
System.out.println("bundleName: " + bundleName)

version = isRelease ? jarVersion : jarVersion + "-SNAPSHOT" // version is the variable the gradle uses.

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = targetJavaVersion
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    maven { url="https://repo1.maven.org/maven2/" }
    maven { url="https://central.sonatype.com/repository/maven-snapshots/" }
}

dependencies {
    api 'io.nats.nkeys:core' + dependencyEnd + ':3.0.3'
    api 'io.nats:jnats-json' + dependencyEnd + ':3.0.5'
    api 'org.jspecify:jspecify:1.0.0'

    testImplementation 'io.nats.nkeys:lts' + dependencyEnd + ':3.0.3'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.14.1'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:4.3.1'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('bundle', Bundle) {
    from sourceSets.main.output
}

jar {
    bundle {
        bnd("Bundle-Name": bundleName,
                "Bundle-Vendor": "nats.io",
                "Bundle-Description": "JWT Utilities",
                "Bundle-DocURL": "https://github.com/nats-io/jwt.java",
                "Target-Compatibility": "Java " + targetJavaVersion
        )
    }
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
        events "started", "passed", "skipped", "failed"
        showStandardStreams = true
    }
}

javadoc {
    failOnError = false
    options.overview = 'src/main/javadoc/overview.html' // relative to source root
    source = sourceSets.main.allJava
    title = "JWT Utilities"
    classpath = sourceSets.main.runtimeClasspath
}

jacoco {
    toolVersion = "0.8.13"
}

jacocoTestReport {
    reports {
        xml.required = true // coveralls plugin depends on xml format report
        html.required = true
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            username = System.getenv('OSSRH_USERNAME')
            password = System.getenv('OSSRH_PASSWORD')
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'JWT Utilities'
                description = 'JWT Utilities for NATS'
                url = 'https://github.com/nats-io/jwt.java'

                groupId = group
                artifactId = artifact

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'synadia'
                        name = 'Synadia'
                        email = 'info@synadia.com'
                        url = 'https://synadia.io'
                    }
                }
                scm {
                    url = 'https://github.com/nats-io/jwt.java'
                }
            }
        }
    }
}

if (isRelease) {
    signing {
        def signingKeyId = System.getenv('SIGNING_KEY_ID')
        def signingKey = System.getenv('SIGNING_KEY')
        def signingPassword = System.getenv('SIGNING_PASSWORD')
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign configurations.archives
        sign publishing.publications.mavenJava
    }
}
